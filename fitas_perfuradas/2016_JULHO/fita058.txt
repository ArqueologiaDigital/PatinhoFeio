10  REM  **  TIMBRE P/ SINTETIZADOR  **
20  DIM A$[72]
30  DIM A[1000],F[1000],T[32]
40  LET M0=M9=0
50  LET P=3.14159/32
60  RESTORE 2000
70  READ N,F0
80  RESTORE 3000
90  MAT  READ A[N]
100  IF F0#0 THEN 130
110  MAT F=ZER
120  GOTO 150
130  RESTORE 4000
140  MAT  READ F[N]
150  FOR I=0 TO 31
160  LET V=0
170  LET X=(2*I+1)*P
180  FOR J=1 TO N
190  LET V=V+A[J]*SIN(J*X+F[J])
200  NEXT J
210  LET T[I+1]=V
220  LET M9=M9 MAX V
230  LET M0=M0 MIN V
240  NEXT I
250  LET M0=ABS(M0)
260  LET D=M9+M0
270  RESTORE 1000
280  READ A$
290  PRINT TAB(15);"INSTRUMENTO :   ";A$
300  PRINT ""
310  PRINT "CHAVE PASSO";TAB(24);"FORMATO"
320  FOR I=1 TO 16
330  LET T[I]=INT(((T[I]+M0)/D*150+5)/10)
340  PRINT I;T[I];TAB(2*T[I]+20);"*"
350  NEXT I
360  PRINT 
370  PRINT 
380  PRINT 
390  PRINT "AMPLITUDE DAS HARMONICAS :"
400  FOR I=1 TO N
410  PRINT A[I];
420  NEXT I
430  PRINT ""
440  IF F0#0 THEN 470
450  PRINT "SEM DEFASAGEM"
460  STOP 
470  PRINT "FASES INICIAS EM GRAUS :"
480  FOR I=1 TO N
490  PRINT F[I];
500  NEXT I
510  PRINT ""
520  STOP 
1000  DATA "FLAUTA ( NOTAS ALTAS )"
2000  DATA 2,0
3000  DATA 100,10
4000  DATA 0
9999  END 
