10  REM CALCULO DE CAPACITORES, INDUTORES E TRILHAGEM P/ SINTONIA"
20  LET P1=3.1416
30  PRINT "FREQ.NOM. MINIMO,MAXIMO E PASSO";
40  INPUT F1,F2,D
50  PRINT "VALOR DE F.I.";
60  INPUT F
70  RESTORE 1000
80  READ C0,C1
90  LET X1=(F2/F1)^2
100  LET T=(C1-X1*C0)/(X1-1)
110  LET L=1/(4*P1^2*(T+C1)*F1^2)
120  LET S0=1/(4*P1^2*(F2+F)^2*L)
130  LET S1=1/(4*P1^2*(F1+F)^2*L)
140  LET V0=S1-S0
150  LET D1=(1/C0+1/C1)^2-4*(1/(C1*C0)-1/V0*(1/C0-1/C1))
160  IF D1<0 THEN 350
170  LET S=2/(SQR(D1)-(1/C0+1/C1))
180  IF S<0 THEN 350
190  LET P=S1-S*C1/(S+C1)
200  PRINT "VALOR DO INDUTOR: ";L
210  PRINT "CAP.OSC. SERIE: ";S;"CAP.OSC. PARALELO: ";P
220  PRINT "CAPACITOR DE SINTONIA PARALELO: ";T
230  PRINT ""
240  PRINT " F. NOM.         F. OSC.       F. CENTR.     % TRACK.";
250  PRINT "        CAP."
260  PRINT ""
270  FOR F3=F1 TO F2 STEP D
280  LET C3=1/(4*P1^2*L*F3^2)
290  LET C4=C3*S/(C3+S)+P
300  LET F0=1/(2*P1*SQR(L*C4))
310  LET K=100*(F0-F3-F)/F3
320  PRINT F3,F0,F0-F,K,C3
330  NEXT F3
340  STOP 
350  PRINT "VALORES INCOERENTES"
360  STOP 
1000  DATA 30,420
1500  END 
