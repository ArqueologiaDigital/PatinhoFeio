~~10  DIM A[10],B[10],R$[70],S$[9]
20  PRINT TAB(15);"GRAND PRIX BRASIL"
30  PRINT "QUANTAS PESSOAS VAO JOGAR (NO MAXIMO 10)";
40  INPUT N
50  PRINT "PILOTOS-ESCREVAM SEU NOME (ATE 7 LETRAS)"
60  FOR J=1 TO N
70  INPUT R$[7*J-6,7*J]
80  NEXT J
90  MAT A=ZER
95 PRINT "VAI COMECAR"
100  MAT B=CON
105  FOR W=1 TO 150
110  FOR I=1 TO N
120  GOTO B[I] OF 130,840,820,870
130  X=INT(6*RND(0))+1
140  PRINT R$[7*I-6,7*I]" TIROU"X
150  K=200-A[I]-X
160  IF K#100 THEN 190
170  PRINT "GANHOU"
180  GOTO 890
190  IF K>100 THEN 260
200  IF K#A[I] THEN 230
210  PRINT "FICA NO"K
220  GOTO 280
230  IF K>A[I] THEN 270
240  PRINT "VOLTA PARA O"K
250  GOTO 280
260  K=A[I]+X
270  PRINT "VAI PARA"K
280  A[I]=K
290  IF A[I]#9 THEN 330
300  K=5
310  PRINT "PESSOAS NA PISTA ";
320  GOTO 240
330  IF A[I]#15 THEN 370
340  X=4
350  PRINT "SEU CARRO ESTA JOIA ";
360  GOTO 260
370  IF A[I]#18 THEN 410
390  PRINT "VOCE BATEU O RECORDE;JOGUE NOVAMENTE"
400  GOTO 130
410  IF A[I]#26 THEN 450
420  B[I]=4
430  PRINT "FUNDIU O MOTOR-DESISTA"
450  IF A[I]#31 THEN 490
460  K=28
470  PRINT "OLEO NA PISTA ";
480  GOTO 240
490  IF A[I]#39 THEN 530
500  B[I]=3
510  PRINT "FUROU UM PNEU-PERDE DUAS JOGADAS "
530  IF A[I]#45 THEN 580
540  X=5
550  PRINT "VOCE ACHOU O LIMITE DO CARRO ";
570  GOTO 260
580  IF A[I]#53 THEN 620
590  K=50
600  PRINT "SAIU DA PISTA ";
610  GOTO 240
620  IF A[I]#62 THEN 660
630  PRINT "ACELERE AGORA E JOGUE NOVAMENTE"
640  GOTO 130
660  IF A[I]#71 THEN 700
670  X=2
680  PRINT "ULTRAPASSE AGORA ";
690  GOTO 260
700  IF A[I]#80 THEN 740
710  B[I]=2
720  PRINT "ACABOU A GASOLINA-PERDE 1 JOGADA"
740  IF A[I]#88 THEN 780
750  X=5
760  PRINT "E A SUA ULTIMA CHANCE ";
770  GOTO 260
780  IF A[I]#96 THEN 860
790  B[I]=4
800  PRINT "QUEBROU A SUSPENSAO-DESISTA"
810  GOTO 860
820  B[I]=B[I]-1
830  GOTO 870
840  B[I]=1
850  GOTO 870
860  PRINT ""
870  NEXT I
879  NEXT W
890  PRINT "QUEREM JOGAR NOVAMENTE";
900  INPUT S$
910  IF S$="SIM" THEN 90
920  PRINT "************ TCHAU ************"
930  END 
