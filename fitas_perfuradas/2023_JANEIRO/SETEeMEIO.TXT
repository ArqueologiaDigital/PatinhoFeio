1  DIM B[10],R$[3],Q$[70],G$[10]
2  MAT B=ZER
3  PRINT TAB(20),"JOGO SETE E MEIO"
5  PRINT "INSTRUCOES:ESCREVA AS RESPOSTAS DEPOIS DA INTERROGACAO"
7  PRINT TAB(11)"AS PALAVRAS SIM E NAO DEVEM VIR ENTRE ASPAS"
9  PRINT TAB(11)"A SEGUIR APERTE A TECLA COM SINAL </ (FICA A DIREITA)"
10  PRINT TAB(11)"PODEM PARTICIPAR ATE 10 PESSOAS"
11  PRINT "QUANTAS PESSOAS VAO JOGAR"
12  INPUT P
13  PRINT "JOGADORES , ESCREVAM SEU NOME (ATE 7 LETRAS)"
14  FOR M=1 TO P
15  INPUT Q$[7*M-6,7*M]
16  NEXT M
17  G$="SSSSSSSSSS"
18  G$[1,P]="AAAAAAAAAA"
19  FOR I=1 TO P
20  IF G$[I,I]="S" THEN 190
30  PRINT Q$[7*I-6,7*I]"QUER CARTAS"
40  INPUT R$
50  IF R$="SIM" THEN 110
60  IF R$="NAO" THEN 90
70  PRINT "ESCREVA A RESPOSTA CERTA...EH MOBRAL!!!"
80  GOTO 40
90  PRINT "SO CONSEGUIU";B[I]" PONTOS"
95  G$[I,I]="S"
100  GOTO 190
110  K=INT(9*RND(1))+1
120  IF K<8 THEN 140
130  K=.5
140  PRINT "A  CARTA VALE "K
150  B[I]=B[I]+K
160  IF B[I]<7.5 THEN 190
170  IF B[I]=7.5 THEN 185
175  PRINT "ESTOUROU"
180  GOTO 95
185  PRINT "PARABENS, CONSEGUIU GANHAR"
187  GOTO 95
190  NEXT I
200  IF G$<"SSSSSSSSSS" THEN 19
270  END 
