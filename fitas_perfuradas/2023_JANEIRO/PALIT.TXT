10  REM  *****  PALIT  *****  DEMONSTRATION PROGRAM  *****
20  REM  *****  VERSION 1  *****  LSD-02/15/73  *****
30  DIM A[3],R$[9]
40  DATA 0,0,1,0,1,0,0,2,2,2,2,0,0,3,3,3,3,0,0,4,4,4,4,0,0,5,5,5,5,0,1,1
50  DATA 1,1,2,3,1,3,2,2,3,1,1,4,5,1,5,4,4,5,1,3,5,4,3,4,5,2,6,4,2,4,6
60  PRINT TAB(20);"JOGO PALITECNICO"
70  PRINT "INSTRUCOES-O JOGO CONSTA DE 3 LINHAS COM 3,5 E 7 PALITOS"
80  PRINT TAB(11);"CADA JOGADOR RETIRA QUANTOS PALITOS QUIZER DE 1 LINHA"
90  PRINT TAB(11)"QUEM FICAR COM O ULTIMO PALITO PERDE O JOGO"
100  PRINT "EU COMECO"
110  A[1]=3
120  A[2]=5
130  A[3]=7
140  X=INT(3*RND(0))+1
150  IF X=4 THEN 140
160  A[X]=A[X]-1
170  GOTO 450
180  PRINT "AGORA E A SUA VEZ-";
190  PRINT "EM QUE LINHA";
200  INPUT B
210  PRINT "QUANTOS PALITOS ";
220  INPUT C
230  A[B]=A[B]-C
240  IF A[B] >= 0 AND C >= 0 THEN 280
250  A[B]=A[B]+C
260  PRINT "JOGUE DIREITO-ASSIM NAO VALE"
270  GOTO 190
280  READ E,P,S
290  FOR T=1 TO 2
300  IF A[1]#E OR A[2]#P THEN 330
310  A[3]=S
320  GOTO 440
330  IF A[1]#E OR A[3]#P THEN 360
340  A[2]=S
350  GOTO 440
360  IF A[2]#E OR A[3]#P THEN 390
370  A[1]=S
380  GOTO 440
390  H=E
400  E=P
410  P=H
420  NEXT T
430  GOTO 280
440  RESTORE 
450  PRINT "MINHA JOGADA"
460  R$="   "
470  FOR Z=1 TO 3
480  PRINT R$[1,4-Z];
490  IF A[Z] <= 0 THEN 530
500  FOR W=1 TO A[Z]
510  PRINT "/";
520  NEXT W
530  PRINT 
540  NEXT Z
550  IF A[1]+A[2]+A[3]>1 THEN 180
560  PRINT "GANHEI"
570  PRINT "QUER JOGAR NOVAMENTE";
580  INPUT R$
590  IF R$="SIM" THEN 100
600  PRINT "NAO ADIANTAVA MESMO CONTINUAR;EU SEMPRE GANHO"
610  END 
