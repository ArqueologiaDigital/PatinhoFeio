10  REM-PROGRAMA PARA ATUALIZAR O PROLI1
20  DIM Z$[3],Y$[72],R$[48],D$[62],U$[62],P$[72]
30  FILES A1,A2,A3,A4,A5,A6,A7,A8
40  PRINT "ATUALIZADOR DO PROLI1"
50  PRINT "PARA ATUALIZAR O PROLI2 (HP) CHAME O APOLI2"
60  READ #1,21;E1,M3
70  PRINT "DESEJA ACRESCENTAR ALGUMA AREA";
80  INPUT Z$
90  IF Z$="NAO" THEN 180
100  PRINT "QUANTAS AREAS";
110  INPUT C
120  PRINT "ENTRE COM OS CODIGOS DAS AREAS E SEUS RESPECTIVOS NOMES"
130  FOR L=1 TO C
140  INPUT Y$
150  PRINT #1,E1+L;Y$
160  NEXT L
170  E1=E1+C
180  PRINT "DESEJA ACRESCENTAR ALGUM PROGRAMA";
190  INPUT Z$
200  IF Z$="NAO" THEN 940
210  PRINT "QUANTOS PROGRAMAS ";
220  INPUT C
230  FOR I=1 TO C
240  PRINT "ESCREVA O NOME E O CODIGO DO PROGRAMA,SEPARADOS POR VIRGULA"
250  PRINT "O CODIGO DEVE CONSISTIR DE 3 LETRAS E 3 ALGARISMOS"
260  INPUT R$,D$[1,6]
270  PRINT "O PROGRAMA JA ESTA IMPLANTADO ";
280  INPUT Z$
290  IF Z$="SIM" THEN 310
300  R$[41,41]="N"
310  PRINT "ESCREVA AGORA O NOME DO AUTOR"
320  INPUT P$
330  PRINT "QUANTAS LINHAS DE COMENTARIO (MINIMO 1,MAXIMO 12)"
340  PRINT "LEMBRE-SE QUE SAO 62 CARACTERES POR LINHA"
350  INPUT E
360  T=0
370  J=2
380  F=1
390  READ #J,F;Y$
400  IF Y$[1,3]="FIM" THEN 580
410  FOR X=1 TO 48
420  IF Y$[X,X]#" " THEN 470
430  NEXT X
440  T=T+1
450  IF T>E/2+.5 THEN 540
460  GOTO 490
470  T=0
480  F=F+1
490  IF F<9 THEN 390
500  J=J+1
510  IF J<91 THEN 380
520  PRINT "NAO HA MAIS ESPACO"
530  STOP 
540  F=F-T
550  IF F>0 THEN 580
560  J=J-1
570  F=90-F
580  PRINT #J,F;R$[1,48],P$[1,72],E
590  Y5=1
600  READ #1,22
610  IF M3=0 THEN 790
620  FOR A=1 TO M3
630  READ #1;D$[7,12],A1,A2
640  IF Y5=0 THEN 690
650  IF D$[1,6]#D$[7,12] THEN 680
660  PRINT "ESSE CODIGO JA ESTA GRAVADO"
670  GOTO 940
680  IF D$[1,6]>D$[7,12] THEN 780
690  Y5=0
700  READ #1,22
710  FOR B=2 TO A
720  READ #1;D$[13,18],A3,A4
730  NEXT B
740  PRINT #1;D$[1,6],J,F
750  D$[1,6]=D$[7,12]
760  J=A1
770  F=A2
780  NEXT A
790  PRINT #1;D$[1,6],J,F
800  M3=M3+1
810  PRINT "AGORA ESCREVA OS COMENTARIOS"
820  FOR K=1 TO E+2
830  IF K <= E THEN 860
840  U$="FIM"
850  GOTO 870
860  INPUT U$
870  IF  END #J THEN 890
880  GOTO 910
890  J=J+1
900  PRINT #J,1
910  PRINT #J;U$[1,62]
920  NEXT K
930  NEXT I
940  PRINT "DESEJA APAGAR ALGUM PROGRAMA"
950  INPUT Z$
960  IF Z$="NAO" THEN 1300
970  PRINT "QUANTOS PROGRAMAS ";
980  INPUT C
990  PRINT "ESCREVA OS CODIGOS DOS PROGRAMAS (1 POR LINHA)"
1000  FOR G=1 TO C
1010  INPUT P$
1020  READ #1,22
1030  FOR K=1 TO M3
1040  READ #1;D$[1,6],H,I
1050  IF D$[1,6]#P$ THEN 1260
1060  FOR M=1 TO M3-K
1070  READ #1;Y$[1,6],N,V
1080  READ #1,22
1090  FOR F1=3 TO K+M
1100  READ #1;R$[1,6],S1,S2
1110  NEXT F1
1120  PRINT #1;Y$[1,6],N,V
1130  READ #1;R$[1,6],S1,S2
1140  NEXT M
1150  READ #H,I;R$[1,48],Y$[1,72],X
1160  PRINT #H,I
1170  D$=" "
1180  FOR S=-1 TO X
1190  IF  END #H THEN 1210
1200  GOTO 1230
1210  H=H+1
1220  PRINT #H,1
1230  PRINT #H;D$[1,62]
1240  NEXT S
1250  GOTO 1280
1260  NEXT K
1270  PRINT "NAO EXISTE ESSE CODIGO"
1280  NEXT G
1290  M3=M3-C
1300  PRINT #1,21;E1,M3
1310  END 
