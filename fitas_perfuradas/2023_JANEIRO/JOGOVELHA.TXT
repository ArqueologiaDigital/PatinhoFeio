10  REM *** JOGO DA VELHA *** LSD-POLI-USP  12/10/1972
20  REM-MODIFICADO EM 18/12/72
30  DIM R$[9],A[9]
40  C=0
50  PRINT TAB(20);"JOGO DA VELHA"
60  PRINT "INSTRUCOES:";
70  PRINT "AS RESPOSTAS DEVEM SER DADAS DEPOIS DA INTERROGACAO"
80  PRINT TAB(11)"DEPOIS DE CADA RESPOSTA APERTE A TECLA RETURN"
90  PRINT ""
100  R$="123456789"
110  PRINT "A NUMERACAO DAS CASAS E A SEGUINTE"
120  GOSUB 1100
130  MAT A=ZER
140  R$="    X    "
150  PRINT "EU COMECO"
160  GOSUB 1090
170  A[5]=1
180  FOR I=1 TO 4
190  PRINT "JOGUE VOCE AGORA: EM QUE NUMERO"
200  INPUT N
210  IF N >= 1 AND N <= 9 THEN 240
220  PRINT "JOGUE DIREITO ; ASSIM NAO VALE"
230  GOTO 200
240  IF A[N]=0 THEN 270
250  PRINT "ESTA CASA JA ESTA OCUPADA"
260  GOTO 200
270  R$[N,N]="O"
280  A[N]=-1
290  FOR J=1 TO 3
300  IF A[3*J-2]+A[3*J-1]+A[3*J]=-3 THEN 340
310  IF A[J]+A[3+J]+A[6+J]=-3 THEN 340
320  NEXT J
330  IF A[1]+A[5]+A[9]>-3 AND A[3]+A[5]+A[7]>-3 THEN 360
340  PRINT "PARABENS ; VOCE CONSEGUIU GANHAR"
350  GOTO 800
360  FOR W=2 TO -2 STEP -4
370  IF C>1 THEN 630
380  FOR E=1 TO 7 STEP 3
390  IF A[E]+A[E+1]+A[E+2] <> W THEN 450
400  FOR G=0 TO 2
410  IF ABS(A[E+G])=1 THEN 440
420  B=E+G
430  GOTO 660
440  NEXT G
450  NEXT E
460  FOR F=1 TO 3
470  IF A[F]+A[3+F]+A[6+F] <> W THEN 530
480  FOR H=0 TO 6 STEP 3
490  IF ABS(A[H+F])=1 THEN 520
500  B=F+H
510  GOTO 660
520  NEXT H
530  NEXT F
540  IF A[1]+A[5]+A[9] <> W THEN 580
550  FOR B=1 TO 9 STEP 4
560  IF ABS(A[B]) <> 1 THEN 660
570  NEXT B
580  IF A[3]+A[5]+A[7] <> W THEN 620
590  FOR B=3 TO 7 STEP 2
600  IF ABS(A[B]) <> 1 THEN 660
610  NEXT B
620  NEXT W
630  B=INT(9*RND(0))+1
640  IF B=10 OR A[B] <> 0 THEN 630
650  IF C>0 THEN 680
660  IF I>1 THEN 680
670  GOSUB 1000
680  A[B]=1
690  R$[B,B]="X"
700  GOSUB 1090
710  FOR K=1 TO 3
720  IF A[3*K-2]+A[3*K-1]+A[3*K]=3 THEN 760
730  IF A[K]+A[K+3]+A[6+K]=3 THEN 760
740  NEXT K
750  IF A[1]+A[5]+A[9]<3 AND A[3]+A[5]+A[7]<3 THEN 780
760  PRINT "GANHEI"
770  GOTO 800
780  NEXT I
790  PRINT "EMPATOU"
800  PRINT "QUER JOGAR NOVAMENTE"
810  INPUT R$
820  IF R$="NAO" THEN 980
830  IF R$="SIM" THEN 860
840  PRINT "ESCREVA APENAS SIM OU NAO"
850  GOTO 810
860  IF C>0 THEN 890
870  PRINT "VOCE QUER TER UMA CHANCE DE GANHAR"
880  GOTO 900
890  PRINT "VOCE ACHA QUE AINDA ESTA DIFICIL"
900  INPUT R$
910  PRINT ""
920  IF R$="NAO" THEN 130
930  IF R$="SIM" THEN 960
940  PRINT "ESCREVA APENAS SIM OU NAO"
950  GOTO 900
960  C=C+1
970  GOTO 130
980  PRINT "***** TCHAU *****"
990  STOP 
1000  IF A[1]>-1 AND A[8]>-1 THEN 1020
1010  B=9
1020  IF A[3]>-1 AND A[4]>-1 THEN 1040
1030  B=7
1040  IF A[7]>-1 AND A[6]>-1 THEN 1060
1050  B=3
1060  IF A[9]>-1 AND A[2]>-1 THEN 1080
1070  B=1
1080  RETURN 
1090  PRINT "MINHA JOGADA"
1100  FOR M=3 TO 9 STEP 3
1110  PRINT "I---I---I---I"
1120  PRINT "I "R$[M-2,M-2]" I "R$[M-1,M-1]" I "R$[M,M]" I"
1130  NEXT M
1140  PRINT "I---I---I---I"
1150  PRINT 
1160  RETURN 
1170  END 
