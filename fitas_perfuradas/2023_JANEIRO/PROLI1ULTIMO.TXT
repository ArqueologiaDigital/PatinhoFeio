10  REM-PROG.PARA LISTAR OS PROG.EXISTENTES NOS COMPUTADORES DO LSD
20  DIM Z$[3],Y$[72],R$[48],D$[62],U$[62],P$[72]
30  FILES A1,A2,A3,A4,A5,A6,A7,A8
40  READ #1,21;E1,M3
50  PRINT ""
60  PRINT "LISTAGEM DOS PROGRAMAS EXISTENTES NO 1130"
70  PRINT "PARA OS PROGRAMAS DO HP CHAME O PROGRAMA PROLI2"
80  PRINT "DESEJA LISTAGEM DAS AREAS";
90  INPUT Z$
100  IF Z$="NAO" THEN 170
110  PRINT "CODIGO  NOME"
120  FOR H=1 TO E1
130  READ #1,H;Y$
140  PRINT "  ";
150  PRINT Y$
160  NEXT H
170  PRINT 
180  PRINT "QUAL O CODIGO DA AREA DE SEU INTERESSE"
190  INPUT U$
200  READ #1,22
210  PRINT ""
220  PRINT "RELACAO DOS PROGRAMAS EXISTENTES NESSA AREA"
230  PRINT 
240  FOR J=1 TO M3
250  READ #1;D$[1,6],E,Q
260  IF D$[1,3]#U$[1,3] THEN 300
270  READ #E,Q;R$,P$
280  PRINT D$[4,6],R$[1,40]
290  PRINT "  ";
300  NEXT J
310  PRINT 
320  PRINT "INFORMACOES SOBRE CADA UM DESSES PROGRAMAS PODEM SER OBTIDAS"
330  PRINT "ENTRANDO COM O SEU RESPECTIVO NUMERO (3 ALGARISMOS)"
340  PRINT "SE NENHUM ENTRE COM 000"
350  INPUT Z$
360  IF Z$="000" THEN 670
370  READ #1,22
380  FOR O=1 TO M3
390  READ #1;D$[1,6],E,Q
400  IF D$[4,6]#Z$ THEN 420
410  IF D$[1,3]=U$[1,3] THEN 450
420  NEXT O
430  PRINT "NUMERO INVALIDO"
440  GOTO 650
450  PRINT TAB(40);"IBM 1130-16K";
460  READ #E,Q;R$,P$,E5
470  PRINT "LSD-EPUSP-DEETITULO-";R$[1,40]
480  PRINT "CODIGO- "D$[1,6]
490  PRINT "AUTOR-"P$[1,64]
500  PRINT "  ";
510  IF R$[41,41]#"N" THEN 540
520  PRINT "PROGRAMA NAO IMPLANTADO"
530  GOTO 550
540  PRINT "PROGRAMA IMPLANTADO"
550  FOR S=1 TO E5
560  IF  END #E THEN 580
570  GOTO 600
580  E=E+1
590  READ #E,1
600  READ #E;D$[1,62]
610  PRINT D$
620  PRINT "  ";
630  NEXT S
640  PRINT ""
650  PRINT "SE DESEJAR MAIS ALGUM,ENTRE COM SEU NUMERO ";
660  GOTO 340
670  END 
