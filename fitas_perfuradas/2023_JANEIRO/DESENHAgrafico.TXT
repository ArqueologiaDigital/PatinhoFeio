10  REM ***** ESTE PROGRAMA TRACA UM GRAFICO DE PONTOS FORNECIDOS *****
20  REM ***** LSD-EPUSP-DEE  *  11/9/73 *****
30  DIM A$[72],M[1000]
40  A$[1,72]=" "
50  READ N
60  MAT  READ M[N]
70  IF TYP(0)=3 THEN 100
80  PRINT "FORAM FORNECIDOS MAIS QUE"N"PONTOS"
90  PRINT "SERAO PLOTADOS APENAS OS"N"PRIMEIROS"
100  M1=M2=M[1]
110  FOR I=2 TO N
120  IF M[I] <= M1 THEN 140
130  M1=M[I]
140  IF M[I] >= M2 THEN 160
150  M2=M[I]
160  NEXT I
170  PRINT "O VALOR MAXIMO E' "M1
180  PRINT "O VALOR MINIMO E' "M2
190  IF M1 >= 0 THEN 210
200  M1=0
210  IF M2 <= 0 THEN 230
220  M2=0
230  PRINT "O ESPACAMENTO DO EIXO Y VALE";(M1-M2)/72
240  PRINT ""
250  E=71*M2/(M2-M1)+1
260  FOR I=1 TO N
270  A$[E,E]="+"
280  T=(M[I]-M2)*71/(M1-M2)+1
290  A$[T,T]="*"
300  X=E
310  IF T <= E THEN 330
320  X=T
330  PRINT A$[1,X]
340  A$[1,72]=" "
350  NEXT I
360  PRINT TAB(E);"X"
370  DATA 10
380  DATA 1,2,3,4,5,6,7,8,9,8,1,1,2
390  END 
