5 REM PALITOS-26/2/72
10 DIM A(3),R$(9)
20 DATA 0,0,1,0,1,0,0,2,2,2,2,0,0,3,3,3,3,0,0,4,4,4,4,0,0,5,5,5,5,0,1,1
30 DATA 1,1,2,3,1,3,2,2,3,1,1,4,5,1,5,4,4,5,1,3,5,4,3,4,5,2,6,4,2,4,6
40 PRINT TAB(20);"JOGO PALITECNICO"
50 PRINT "INSTRUCOES-O JOGO CONSTA DE 3 LINHAS COM 3,5 E 7 PALITOS"
60 PRINT TAB(11);"CADA JOGADOR RETIRA QUANTOS PALITOS QUIZER DE 1 LINHA"
70 PRINT TAB(11);"QUEN FICAR COM O ULTIMO PALITO PERDE O JOGO"
80 PRINT "EU COMECO"
90 A(1)=3
100 A(2)=5
110 A(3)=7
120 X=INT(3*RND(0))+1
130 IF X=4 THEN 120
140 A(X)=A(X)-1
150 GOTO 410
160 PRINT "AGORA E A SUA VEZ-EM QUE LINHA ";
170 INPUT B
180 PRINT "QUANTOS PALITOS ";
190 INPUT C
200 A(B)=A(B)-C
210 IF A(B)>=0 AND C>=0 THEN 240
220 PRINT "JOGUE DIREITO-ASSIM NAO VALE"
230 GOTO 180
240 READ E,P,S
250 FOR T=1 TO 2
260 IF A(1)#E OR A(2)#P THEN 290
270 A(3)=S
280 GOTO 400
290 IF A(1)#E OR A(3)#P THEN 320
300 A(2)=S
310 GOTO 400
320 IF A(2)#E OR A(3)#P THEN 350
330 A(1)=S
340 GOTO 400
350 H=E
360 E=P
370 P=H
380 NEXT T
390 GOTO 240
400 RESTORE
410 PRINT "MINHA JOGADA"
420 FOR Z=1 TO 3
430 IF A(Z)<=0 THEN 480
440 FOR W=1 TO A(Z)
450 PRINT"/";
460 NEXT W
470 PRINT
480 NEXT Z
490 IF A(1)+A(2)+A(3)>1 THEN 160
500 PRINT "GANHEI"
510 PRINT "QUER JOGAR NOVAMENTE";
520 INPUT R$
530 IF R$="SIM" THEN 80
540 PRINT "NAO ADIANTAVA MESMO CONTINUAR;EU SEMPRE GANHO"
550 END
